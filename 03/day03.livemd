# Day 03 Gear Ratios

## Part 1

You and the Elf eventually reach a gondola lift station; he says the gondola lift will take you up to the water source, but this is as far as he can bring you. You go inside.

It doesn't take long to find the gondolas, but there seems to be a problem: they're not moving.

* "Aaah!"

You turn around to see a slightly-greasy Elf with a wrench and a look of surprise. "Sorry, I wasn't expecting anyone! The gondola lift isn't working right now; it'll still be a while before I can fix it." You offer to help.

The engineer explains that an engine part seems to be missing from the engine, but nobody can figure out which one. If you can add up all the part numbers in the engine schematic, it should be easy to work out which part is missing.

The engine schematic (your puzzle input) consists of a visual representation of the engine. There are lots of numbers and symbols you don't really understand, but apparently any number adjacent to a symbol, even diagonally, is a "part number" and should be included in your sum. (Periods (.) do not count as a symbol.)

Here is an example engine schematic:

```
467..114..
...*......
..35..633.
......#...
617*......
.....+.58.
..592.....
......755.
...$.*....
.664.598..
```

In this schematic, two numbers are not part numbers because they are not adjacent to a symbol: 114 (top right) and 58 (middle right). Every other number is adjacent to a symbol and so is a part number; their sum is 4361.

Of course, the actual engine schematic is much larger. What is the sum of all of the part numbers in the engine schematic?

```elixir
input = File.stream!(Path.join(__DIR__, "input.txt"))
example = File.stream!(Path.join(__DIR__, "example.txt"))
```

```elixir
input_with_indices = input |> Stream.map(&String.trim(&1)) |> Enum.with_index()
```

```elixir
defmodule A do
  @doc """
  iex> A.get_start_parts("x1xx123xx45")
  [{1, 1}, {4, 3}, {9, 2}]
  """

  def get_start_parts(input) do
    Regex.scan(~r/\d+/, input, return: :index)
    |> List.flatten()
  end

  def get_symbols(input) do
    input
    |> A.get_all_by_pattern(~r/[^a-zA-Z0-9\.]/)
    |> MapSet.new()
  end

  def get_all_by_pattern(input, pattern) do
    input
    |> Stream.flat_map(fn {line, i} ->
      Regex.scan(pattern, line, return: :index)
      |> List.flatten()
      |> Enum.map(fn {j, _} -> {i, j} end)
    end)
  end

  def get_part_ranges(input) do
    input
    |> Enum.flat_map(fn {line, i} ->
      A.get_start_parts(line)
      |> Enum.map(fn {j, len} ->
        {
          String.to_integer(String.slice(line, j, len)),
          (i - 1)..(i + 1),
          (j - 1)..(j + len)
        }
      end)
    end)
  end
end
```

```elixir
parts = A.get_part_ranges(input_with_indices)
symbols = A.get_symbols(input_with_indices)
```

```elixir
parts
|> Stream.filter(fn {_length, range_y, range_x} ->
  for i <- range_y, j <- range_x, reduce: false do
    acc -> acc || {i, j} in symbols
  end
end)
|> Stream.map(&elem(&1, 0))
|> Enum.sum()
```

## Part 2

The engineer finds the missing part and installs it in the engine! As the engine springs to life, you jump in the closest gondola, finally ready to ascend to the water source.

You don't seem to be going very fast, though. Maybe something is still wrong? Fortunately, the gondola has a phone labeled "help", so you pick it up and the engineer answers.

Before you can explain the situation, she suggests that you look out the window. There stands the engineer, holding a phone in one hand and waving with the other. You're going so slowly that you haven't even left the station. You exit the gondola.

The missing part wasn't the only issue - one of the gears in the engine is wrong. A gear is any * symbol that is adjacent to exactly two part numbers. Its gear ratio is the result of multiplying those two numbers together.

This time, you need to find the gear ratio of every gear and add them all up so that the engineer can figure out which gear needs to be replaced.

Consider the same engine schematic again:

```
467..114..
...*......
..35..633.
......#...
617*......
.....+.58.
..592.....
......755.
...$.*....
.664.598..
```

In this schematic, there are two gears. The first is in the top left; it has part numbers 467 and 35, so its gear ratio is 16345. The second gear is in the lower right; its gear ratio is 451490. (The * adjacent to 617 is not a gear because it is only adjacent to one part number.) Adding up all of the gear ratios produces 467835.

**What is the sum of all of the gear ratios in your engine schematic?**

```elixir
defmodule B do
  def find_gears(input) do
    input
    |> A.get_all_by_pattern(~r/\*/)
  end
end
```

```elixir
gear_ratio_sum =
  B.find_gears(input_with_indices)
  |> Stream.map(fn {i, j} ->
    case Enum.filter(parts, fn {_length, range_y, range_x} -> i in range_y and j in range_x end) do
      [first, second] -> elem(first, 0) * elem(second, 0)
      _ -> 0
    end
  end)
  |> Enum.sum()
```
